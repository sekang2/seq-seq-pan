import sys

sys.setrecursionlimit(1000000)

SCRIPT="python3.4 /home/jandrasitsc/scripts/multiplegenomealignment_dev/multiplegenomealignment/multiplegenome.py"

with open(config["genomefile"], "r") as f:
    GENOMES = f.readlines()

#GENOME_DICT = {line.split(";")[1].strip() : line.split(";")[0] for line in GENOMES}
#GENOME_NAMES = [line.split(";")[1].strip() for line in GENOMES]

GENOME_DICT = {str(idx): {"name": GENOMES[idx].split(";")[1].strip(), "file":GENOMES[idx].split(";")[0].strip()} for idx in range(len(GENOMES))}
GENOME_NAMES = [str(idx) for idx in range(len(GENOMES))]

# merge if alignment is not split
# merging is done in splitting step anyway
def split_or_not(wildcards):
    return("add"+wildcards["genome"]+"_split.xmfa" if int(wildcards["genome"]) > 1 else "add"+wildcards["genome"]+"_realign.xmfa")
    

def mauve_input(wildcards):
    
    return (GENOME_DICT[wildcards["genome"]]["file"])
    
    
def consensus_file(wildcards):
    
    newnr = prev_genome(wildcards)
    
    if newnr == "0":
        consgen = GENOME_DICT[newnr]["file"]
    else:
        consgen = "add"+newnr+"_consensus.fasta.blockseparated.fasta"
        
    return consgen


def prev_genome(wildcards):
    return(str(int(wildcards['genome']) - 1))
    
    
rule all:
    input: config["outfilename"]+"_consensus.fasta"
    
    
rule fn_translate:
    input: "add"+str(len(GENOME_NAMES)-1)+"_consensus.fasta.blockseparated.fasta"
    output: config["outfilename"]+"_consensus.fasta"
    shell:
        '''
        name={input}
        out={output}
        name="${{name//_consensus.fasta.blockseparated.fasta}}"
        out="${{out//_consensus.fasta}}"
        cp $name"_consensus.fasta.blockseparated.fasta" $out"_consensus.fasta.blockseparated.fasta"
        cp $name"_consensus.fasta" $out"_consensus.fasta"
        cp $name"_consensus.fasta.idx" $out"_consensus.fasta.idx"
        cp $name"_split.xmfa" $out"_split.xmfa"
        
        sed -i "1 s/$name/$out/g" $out"_consensus.fasta"
        sed -i "1 s/$name/$out/g" $out"_consensus.fasta.blockseparated.fasta"
        sed -i "1 s/$name/$out/" $out"_consensus.fasta.idx"
        sed -i "2 s/$name/$out/" $out"_consensus.fasta.idx"
        '''

        
rule consensus:
    input: split_or_not
    
    output: temp("add{genome}_consensus.fasta.blockseparated.fasta")
    
    shell:
        "{SCRIPT} -x {input} -p . -n add{wildcards.genome} -t consensus"
        

rule split:
    input:  xmfa_prev= lambda wildcards: split_or_not({"genome":prev_genome(wildcards)}), 
            xmfa= "add{genome}_realign.xmfa", 
            consensus=consensus_file
    output: temp("add{genome,\d+}_split.xmfa")
    shell:
        "{SCRIPT} -x {input.xmfa} -p . -n add{wildcards.genome} -t split -c {input.consensus}"
        
        
rule realign:
    input: "add{genome}.xmfa"
    output: temp("add{genome,\d+}_realign.xmfa")
    shell:
        "{SCRIPT} -x {input} -p . -n add{wildcards.genome} -t realign"

        
rule merge:
    input: "add{genome}_realign.xmfa"
    output: temp("add{genome,\d+}_merge.xmfa")
    shell:
        "{SCRIPT} -x {input} -p . -n add{wildcards.genome} -t merge"        
        
        
rule align:
    input: consensus_file, mauve_input
    output: temp("add{genome, \d+}.xmfa")
    log: temp("add{genome}.progressiveMauve.log.txt")
    shell:
        "progressiveMauve {input[0]} {input[1]} --output={output} --max-gapped-aligner-length=900 > {log} "
