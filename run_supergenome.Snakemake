import sys

sys.setrecursionlimit(1000000)

SCRIPT="python3.4 " + srcdir("supergenome.py")

with open(config["genomefile"], "r") as f:
    GENOMES = f.readlines()

#GENOME_DICT = {line.split(";")[1].strip() : line.split(";")[0] for line in GENOMES}
#GENOME_NAMES = [line.split(";")[1].strip() for line in GENOMES]

GENOME_DICT = {str(idx): {"name": GENOMES[idx].split(";")[1].strip(), "file":GENOMES[idx].split(";")[0].strip()} for idx in range(len(GENOMES))}
GENOME_NAMES = [str(idx) for idx in range(len(GENOMES))]

# merge if alignment is not split
# merging is done in splitting step anyway
def split_or_not(wildcards):
    return("add"+wildcards["genome"]+"_split.xmfa" if int(wildcards["genome"]) > 1 else "add"+wildcards["genome"]+"_realign.xmfa")
    

def mauve_input(wildcards):
    
    return (GENOME_DICT[wildcards["genome"]]["file"])
    
    
def consensus_file(wildcards):
    
    newnr = prev_genome(wildcards)
    
    if newnr == "0":
        consgen = [GENOME_DICT[newnr]["file"]]
    else:
        consgen = ["add"+newnr+"_consensus.fasta.blockseparated.fasta", "add"+newnr+"_consensus.fasta", "add"+newnr+"_consensus.fasta.blockseparated.idx"]
        
    return consgen


def prev_genome(wildcards):
    return(str(int(wildcards['genome']) - 1))
    
    
rule all:
    input:  config["outfilename"]+"_consensus.fasta.blockseparated.fasta",
            config["outfilename"]+"_consensus.fasta.idx",
            config["outfilename"]+"_consensus.fasta",
            config["outfilename"]+"_split.xmfa",
            config["outfilename"]+"_split.maf"            
    
rule fn_translate:
    input:  cb = "add"+str(len(GENOME_NAMES)-1)+"_consensus.fasta.blockseparated.fasta",
            ci = "add"+str(len(GENOME_NAMES)-1)+"_consensus.fasta.idx",
            c = "add"+str(len(GENOME_NAMES)-1)+"_consensus.fasta",
            xmfa = "add"+str(len(GENOME_NAMES)-1)+"_split.xmfa"
            
    output: cb = config["outfilename"]+"_consensus.fasta.blockseparated.fasta",
            ci = config["outfilename"]+"_consensus.fasta.idx",
            c = config["outfilename"]+"_consensus.fasta",
            xmfa = config["outfilename"]+"_split.xmfa"
            
    shell:
        '''
        name={input.c}
        out={output.c}
        name="${{name//_consensus.fasta}}"
        out="${{out//_consensus.fasta}}"
        cp {input.cb} {output.cb}
        cp {input.ci} {output.ci}
        cp {input.c} {output.c}
        cp {input.xmfa} {output.xmfa}
        
        sed -i "1 s/$name/$out/g" {output.c}
        sed -i "1 s/$name/$out/g" {output.cb}
        sed -i "1 s/$name/$out/" {output.ci}
        sed -i "2 s/$name/$out/" {output.ci}
        '''

        
rule consensus:
    input: split_or_not
    
    output: temp("add{genome}_consensus.fasta.blockseparated.fasta"),
            temp("add{genome}_consensus.fasta.blockseparated.idx"),
            temp("add{genome}_consensus.fasta.idx"),
            temp("add{genome}_consensus.fasta")
    
    shell:
        "{SCRIPT} -x {input} -p . -n add{wildcards.genome} -t consensus"
        

rule split:
    input:  xmfa_prev= lambda wildcards: split_or_not({"genome":prev_genome(wildcards)}), 
            xmfa= "add{genome}_realign.xmfa", 
            consensus=consensus_file
    output: temp("add{genome,\d+}_split.xmfa")
    shell:
        "{SCRIPT} -x {input.xmfa} -p . -n add{wildcards.genome} -t split -c {input.consensus[1]}"
        
        
rule realign:
    input: "add{genome}.xmfa"
    output: temp("add{genome,\d+}_realign.xmfa")
    shell:
        "{SCRIPT} -x {input} -p . -n add{wildcards.genome} -t realign"

        
rule merge:
    input: "add{genome}_realign.xmfa"
    output: temp("add{genome,\d+}_merge.xmfa")
    shell:
        "{SCRIPT} -x {input} -p . -n add{wildcards.genome} -t merge"        
        
        
rule align:
    input:  consensus=consensus_file, 
            new=mauve_input
    output: temp("add{genome, \d+}.xmfa"), 
            temp("add{genome, \d+}.xmfa.bbcols"), 
            temp("add{genome, \d+}.xmfa.backbone")
    shell:
        '''
        input=`basename {input.consensus[0]}`
        input2=`basename {input.new}`
        
        input=temp_$input
        input2=temp_$input2
        
        cp {input.consensus[0]} $input
        cp {input.new} $input2
        
        progressiveMauve $input $input2 --output={output[0]} --max-gapped-aligner-length=900 > /dev/null
        
        sed -i "s|$input|{input.consensus[0]}|g" {output[0]}
        sed -i "s|$input2|{input.new}|g" {output[0]}
        
        rm $input.sslist
        rm $input
        rm $input2.sslist
        rm $input2
        '''
        
        
rule maf:
    input: "{genome}.xmfa"
    output: "{genome}.maf"
    shell:
        "{SCRIPT} -x {input} -p . -n {wildcards.genome} -t maf"
